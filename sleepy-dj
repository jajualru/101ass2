;; The first three lines of this file were inserted by DrRacket. They record metadata
;; about the language level of this file in a form that our tools can easily process.
#reader(lib "htdp-intermediate-reader.ss" "lang")((modname sleepy-dj) (read-case-sensitive #t) (teachpacks ()) (htdp-settings #(#t constructor repeating-decimal #f #t none #f () #f)))
(require rsound)
(require 2htdp/image)
(require 2htdp/universe)

;; a ws is
;; - (make-ws slider-frac end-frame)
;; where slider-frac is a number between
;; zero and one, and end-frame is a frame
;; number
(define-struct ws [slider-frac end-frame])

(define ps (make-pstream))

;; examples:
(make-ws 0.5 2000)
(make-ws 0.7 80000)

(define INITIAL-STATE
  (make-ws 0.0 0))

(define song
  (resample-to-rate
   FRAME-RATE
   (rs-read/clip "Powerful.wav"
                 0
                 (* 20 FRAME-RATE))))

(define BOX-WIDTH 600)
(define BOX-HEIGHT 100)
(define SLIDER-Y (/ BOX-HEIGHT 2))
(define SLIDER-WIDTH 30)
(define SLIDER-HEIGHT 55)
(define SLIDER (rectangle SLIDER-WIDTH
                          SLIDER-HEIGHT
                          "solid"
                          "magenta"))
(define SLIDER-BG (rectangle BOX-WIDTH
                             BOX-HEIGHT
                             "solid"
                             "turquoise"))
;; draw the slider
;; ws -> image
(define (draw-slider ws)
  (place-image SLIDER
               (* (ws-slider-frac ws)
                  BOX-WIDTH)
               SLIDER-Y
               SLIDER-BG))

(check-expect
 (draw-slider (make-ws 0.5 1234))
 (place-image SLIDER
              300 SLIDER-Y
              SLIDER-BG))

;; handle a mouse action; on a drag or
;; click, update the slider position
;; ws number number event -> ws
(define (handle-mouse ws x y event)
  (cond [(string=? event "button-down")
         (make-ws (/ x BOX-WIDTH)
                  (ws-end-frame ws))]
        [(string=? event "drag")
         (make-ws (/ x BOX-WIDTH)
                  (ws-end-frame ws))]
        [else ws]))

(check-expect
 (handle-mouse (make-ws 0.6 1234)
               150 44 "move")
 (make-ws 0.6 1234))
(check-expect
 (handle-mouse (make-ws 0.6 4243)
               150 44 "drag")
 (make-ws (/ 150 BOX-WIDTH) 4243))
(check-expect
 (handle-mouse (make-ws 0.6 1123)
               150 44 "button-down")
 (make-ws (/ 150 BOX-WIDTH) 1123))

;; if it's time, queue up the next section
;; of the song
(define (tick-fun ws)
  (cond [(time-to-play? (ws-end-frame ws)
                        (pstream-current-frame ps))
         (both
          (queue-next-fragment
           (round (* (ws-slider-frac ws)
                     (rs-frames song)))
           (ws-end-frame ws))
          (make-ws (+ (ws-slider-frac ws) PLAY-POSNFRAC)
                   (+ (ws-end-frame ws) PLAY-FRAMES))
          )]
        [else ws]))

;; queue up the next fragment
(define (queue-next-fragment song-frame frame-to-play)
  (pstream-queue ps
                 (clip song song-frame (+ song-frame PLAY-FRAMES))
                 frame-to-play))

;; how long should each queued segment be, in seconds?
(define PLAY-SECONDS 1/20)
;; .. in frames?
(define PLAY-FRAMES (* PLAY-SECONDS FRAME-RATE))
;; .. as a fraction of the slider?
(define PLAY-POSNFRAC (/ PLAY-SECONDS (/ (rs-frames song) FRAME-RATE)))
;; how long should the big-bang ticks be?
(define TICK-LEN 1/40)
;; the longest lead time for which we'll queue the next sound
(define MAX-QUEUE-INTERVAL (* 3/80 FRAME-RATE))



;; is it time to play the next chunk, yet?
(define (time-to-play? end-frame cur-frame)
  (< (- end-frame cur-frame) MAX-QUEUE-INTERVAL))

(define (both a b) b)

(big-bang INITIAL-STATE
          [to-draw draw-slider]
          [on-mouse handle-mouse]
          [on-tick tick-fun TICK-LEN])
